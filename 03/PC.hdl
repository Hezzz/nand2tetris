// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    // increment prev out of register
    Inc16(in=incIn, out=incOut);
    // choose based on conditions
    Mux16(a=incIn,   b=incOut, sel=inc,   out=incOutA);
    Mux16(a=incOutA, b=in,     sel=load,  out=loadOut);
    Mux16(a=loadOut, b=false,  sel=reset, out=regIn);
    // load to the register the resulting output
    Register(in=regIn, load=true, out=out, out=incIn);
}